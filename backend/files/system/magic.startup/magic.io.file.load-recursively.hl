
/*
 * Loads all files recursively from the specified folder.
 * 
 * This slot is used as we scaffold a frontend, since we'll need to
 * recursively load all files in the template folder, before we start
 * transforming the template to an actual result.
 * 
 * Arguments
 * 
 * - 1st argument. Folder from where to load all files recursively from.
 */
slots.create:magic.io.file.load-recursively

   // In case we've got a "slot" type of argument.
   eval:x:@.arguments

   // Lists all files in current [folder], and adds up the result to return value.
   io.file.list-recursively:x:@.arguments/0
      show-hidden:x:@.arguments/*/.hidden-files

   /*
    * Iterating through all files listed above, and loads them,
    * for then to add their contents as a child node of iterated file.
    */
   for-each:x:@io.file.list-recursively/*
      io.file.load:x:@.dp/#
      add:x:@.dp/#
         get-nodes:x:@io.file.load
      set-name:x:@.dp/#/*
         .:

   // Returning to caller all files in folder, and sub folder(s).
   return-nodes:x:@io.file.list-recursively/*
