
/*
 * Lists all tables in your MySQL instance.
 * 
 * Arguments
 * 
 * - [connection-string] - What connection string to use
 * - [database] - What database you want to check
 */
slots.create:magic.db.mysql.tables

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/database

   // Opens up our connection
   .connection:[information_schema]
   if
      and
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .:
      .lambda
         set-value:x:@.connection
            strings.concat
               .:[
               get-value:x:@.arguments/*/connection-string
               .:|
               get-value:x:@.arguments/*/database
               .:]
   else
      set-value:x:@.connection
         strings.concat
            .:[generic|
            get-value:x:@.arguments/*/database
            .:]
   mysql.connect:x:@.connection

      // Runs our query
      mysql.select:"show full tables where Table_Type = 'BASE TABLE'"
      remove-nodes:x:@mysql.select/*/*/Table_type
      set-name:x:@mysql.select/*/*
         .:table
      return-nodes:x:@mysql.select/*
