
/*
 * Lists all tables in your MySQL instance.
 * 
 * Arguments
 * 
 * - [connection-string] - What connection string to use
 * - [database] - What database you want to check
 */
slots.create:magic.db.mssql.tables

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/database

   // Opens up our connection
   .connection:[master]
   if
      and
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .:
      .lambda
         set-value:x:@.connection
            strings.concat
               .:[
               get-value:x:@.arguments/*/connection-string
               .:|
               get-value:x:@.arguments/*/database
               .:]
   else
      set-value:x:@.connection
         strings.concat
            .:[generic|
            get-value:x:@.arguments/*/database
            .:]
   mssql.connect:x:@.connection

      // Runs our query
      mssql.select:@"select table_schema + '.' + table_name as ""table""
   from information_schema.tables
   where table_type='base table' and table_name != 'sysdiagrams'"
      return-nodes:x:@mssql.select/*
