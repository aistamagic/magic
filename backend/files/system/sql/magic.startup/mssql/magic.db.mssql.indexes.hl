
/*
 * Lists all indexes in some SQL Server table.
 * 
 * Arguments
 * 
 * - [connection-string] - What connection string to use
 * - [database] - What database you want to check
 * - [table] - What table you want to check
 */
slots.create:magic.db.mssql.indexes

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/database
   validators.mandatory:x:@.arguments/*/table

   // Opens up our connection
   .connection:[information_schema]
   if
      and
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .:
      .lambda
         set-value:x:@.connection
            strings.concat
               .:[
               get-value:x:@.arguments/*/connection-string
               .:|
               get-value:x:@.arguments/*/database
               .:]
   else
      set-value:x:@.connection
         strings.concat
            .:[generic|
            get-value:x:@.arguments/*/database
            .:]
   mssql.connect:x:@.connection

      // Dynamically creating our SQL.
      strings.split:x:@.arguments/*/table
         .:.
      strings.concat
         .:@"
select IndexName = ind.name, ColumnName = col.name from sys.indexes ind 
  inner join sys.index_columns ic on ind.object_id = ic.object_id and ind.index_id = ic.index_id 
  inner join sys.columns col on ic.object_id = col.object_id and ic.column_id = col.column_id 
  inner join sys.tables t on ind.object_id = t.object_id 
where
  ind.is_primary_key = 0 
  and ind.is_unique = 0 
  and ind.is_unique_constraint = 0 
  and t.is_ms_shipped = 0 
  and t.name = '"
         get-value:x:@strings.split/0/-
         .:"'"
      mssql.select:x:@strings.concat

      /*
       * Creating our return value, by projecting it into
       * a generic form, that does not rely upon SQL Server's syntactic
       * details.
       */
      for-each:x:@mssql.select/*
         unwrap:x:+/*/*/*
         add:x:@mssql.connect/*/return-nodes
            .
               .
                  name:x:@.dp/#/*/IndexName
                  columns
         unwrap:x:+/*/*
         add:x:@mssql.connect/*/return-nodes/0/-/*/columns
            .
               .:x:@.dp/#/*/ColumnName
      return-nodes
