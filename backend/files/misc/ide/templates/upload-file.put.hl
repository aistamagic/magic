
/*
 * Upload file endpoint template file.
 * 
 * This file creates an HTTP PUT endpoint that allows you to upload a single
 * file as multipart/form-data and stores it in a specified folder on the server.
 * 
 * NOTICE! This file should probably be consumed as a Macro, and not as a template, since this
 * will correctly decorate your file with the correct properties.
 */
.description:Upload file endpoint, allowing you to upload a file to the backend.
.arguments
   file:*
.type:file-upload
.accept:multipart/form-data

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/file
validators.mandatory:x:@.arguments/*/file/*/name
validators.mandatory:x:@.arguments/*/file/*/stream

// Verifying user has access to invoke endpoint.
auth.ticket.verify:[[auth]]

// Folder where file should be saved.
.folder
set-value:x:@.folder
   strings.concat
      .:/etc/
      .:[[module]]
      .:/

// Verifying the folder exists, and if not, creating it.
if
   not
      io.folder.exists:x:@.folder
   .lambda

      // Need to create folder.
      io.folder.create:x:@.folder

// Saving file with the specified filename to our folder.
.full-path
set-value:x:@.full-path
   strings.concat
      get-value:x:@.folder
      get-value:x:@.arguments/*/file/*/name
io.stream.save-file:x:@.full-path
   get-value:x:@.arguments/*/file/*/stream

// Returning success to caller.
unwrap:x:+/*/filename
return
   result:success
   filename:x:@.full-path
