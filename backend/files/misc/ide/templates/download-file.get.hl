
/*
 * Returns the specified file to caller.
 * 
 * NOTICE! This file should probably be consumed as a Macro, and not as a template, since this
 * will correctly decorate your file with the correct properties.
 */
.description:Returns the specified file to caller
.type:file-download
.arguments
   file:string

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/file

// Verifying user has access to invoke endpoint.
auth.ticket.verify:[[auth]]

// Concatenating folder with specified image.
strings.concat
   .:/etc/
   .:[[module]]
   .:/
   get-value:x:@.arguments/*/file

// Verifying that file exists.
if
   not
      io.file.exists:x:@strings.concat
   .lambda
      throw:Not found
         public:true
         status:404

// Opening up a read stream for file.
io.stream.open-file:x:@strings.concat

// Making sure we get the HTTP headers correctly applied.
strings.concat
   .:"attachment; filename=\""
   get-value:x:@.arguments/*/file
   .:"\""
unwrap:x:+/*/Content-Disposition
response.headers.set
   Content-Type:application/octet-stream
   Content-Disposition:x:@strings.concat

// Returning image to caller.
return:x:@io.stream.open-file
